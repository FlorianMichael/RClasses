subprojects {
    apply plugin: "java-library"
    apply plugin: "maven-publish"
    apply plugin: "signing"

    archivesBaseName = "RClasses-" + project.maven_name
    group = project.maven_group
    version = project.maven_version

    repositories {
        mavenCentral()
    }

    dependencies {
    }

    java {
        withSourcesJar()
        withJavadocJar()

        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    publishing {
        repositories {
            maven {
                name = "reposilite"
                def releasesUrl = "https://maven.lenni0451.net/releases"
                def snapshotsUrl = "https://maven.lenni0451.net/snapshots"
                url = project.version.endsWith("SNAPSHOT") ? snapshotsUrl : releasesUrl

                credentials(PasswordCredentials)
                authentication {
                    basic(BasicAuthentication)
                }
            }
            maven {
                name = "ossrh"
                def releasesUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                def snapshotsUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
                url = project.version.endsWith("SNAPSHOT") ? snapshotsUrl : releasesUrl

                credentials(PasswordCredentials)
                authentication {
                    basic(BasicAuthentication)
                }
            }
        }
        publications {
            maven(MavenPublication) {
                artifactId = project.maven_name
                groupId = project.group
                version = project.version

                from components.java

                pom {
                    name = artifactId
                    description = "Random collection of Java classes and utils in different submodules which together form a commons library"
                    url = "https://github.com/FlorianMichael/RClasses"
                    licenses {
                        license {
                            name = "Apache-2.0 license"
                            url = "https://github.com/FlorianMichael/RClasses/blob/main/LICENSE"
                        }
                    }
                    developers {
                        developer {
                            id = "FlorianMichael"
                        }
                    }
                    scm {
                        connection = "scm:git:git://github.com/FlorianMichael/RClasses.git"
                        developerConnection = "scm:git:ssh://github.com/FlorianMichael/RClasses.git"
                        url = "github.com/FlorianMichael/RClasses"
                    }
                }
            }
        }
    }

    signing {
        setRequired false
        sign configurations.archives
        sign publishing.publications.maven
    }

    project.tasks.withType(PublishToMavenRepository).forEach {
        it.dependsOn(project.tasks.withType(Sign))
    }

}
